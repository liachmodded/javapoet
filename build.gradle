plugins {
    id 'java'
    id 'maven-publish'
}

def ENV = System.getenv()
version = "0.0." + ENV.BUILD_NUMBER ?: 'local'

group = 'com.github.liachmodded'
archivesBaseName = 'javapoet'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'com.google.testing.compile:compile-testing:0.19'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.jimfs:jimfs:1.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.eclipse.jdt.core.compiler:ecj:4.6.1'
}

java {
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.release = 17
    options.encoding = "UTF-8"
}

tasks.withType(Jar) {
    from(file("LICENSE.txt"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        if (ENV.GITHUB_TOKEN) {
            maven {
                name = "GitHub"
                url = uri("https://maven.pkg.github.com/liachmodded/javapoet")
                credentials {
                    username = ENV.GITHUB_ACTOR
                    password = ENV.GITHUB_TOKEN
                }
            }
        }
    }
}
